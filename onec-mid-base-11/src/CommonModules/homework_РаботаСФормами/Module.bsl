
Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;	
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицо(Форма);
		ДобавитьСкидкуИПересчет(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда	
		ВставитьПолеКонтактноеЛицо(Форма); 
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицо(Форма); 
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицо(Форма); 
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицо(Форма); 
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПолеКонтактноеЛицо(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.homework_КонтактноеЛицо";
	
КонецПроцедуры 

Процедура ВставитьПолеКонтактноеЛицо(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.homework_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ДобавитьСкидкуИПересчет(Форма)
	
	ГруппаСкидка = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"),Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСкидка.ОтображатьЗаголовок = Ложь; 
	ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеВвода = Форма.Элементы.Добавить("Скидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.homework_Скидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "СкидкаПриИзменнении");

    	Команда = Форма.Команды.Добавить("Пересчитать");
	Команда.Действие = "КомандаПересчитатьСтроки";	
	Кнопка = Форма.Элементы.Добавить("Пересчитать", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
	Кнопка.ИмяКоманды = "Пересчитать";
	Кнопка.Заголовок = "Пересчитать";
	Кнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;

КонецПроцедуры














